buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.8.+'
    }
}

allprojects {
    group = 'com.vk.androidsdk'
    version = '1.0.0-SNAPSHOT'
}

// Install all dependcies in configuration externalCompile from all subprojects
// to local maven repository (~/.m2/repository)
task installDependenciesToLocalRepo {
    doLast {
        subprojects {
            configurations.findByName('externalCompile')?.resolvedConfiguration?.resolvedArtifacts?.each { artifact ->
                exec {
                    executable = 'mvn'
                    args = [ 'install:install-file', '--quiet', "-Dfile=${artifact.file.absolutePath}", 
                                 "-DgroupId=${artifact.moduleVersion.id.group}",
                                 "-DartifactId=${artifact.moduleVersion.id.name}",
                                 "-Dversion=${artifact.moduleVersion.id.version}",
                                 "-Dpackaging=${artifact.type}" ]
                }
            }
        }
    }
}

// Execute external build with Maven for generating apklib artifacts
task mavenBuild(type: Exec) {
    dependsOn installDependenciesToLocalRepo
    executable = 'mvn'
    args = [ 'clean', 'package', '--quiet' ]
}
